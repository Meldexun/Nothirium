plugins {
    id 'eclipse'
    id 'net.minecraftforge.gradle' version "${forge_gradle_version}"
    id 'org.spongepowered.mixin' version "${mixin_gradle_version}"
}

version = "${mod_stage}" != "" ? "${mc_version}-${mod_version}-${mod_stage}" : "${mc_version}-${mod_version}"
group = "meldexun.${mod_id}"
archivesBaseName = "${mod_name}"

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

configurations {
    atDependencies
    implementation.extendsFrom(atDependencies)

    includedDependencies
    implementation.extendsFrom(includedDependencies)

    buildDependencies
}

repositories {
    ivy {
        url 'https://github.com/'

        patternLayout {
            artifact '[organisation]/[module]/releases/download/v[revision]/[module]-[revision].[ext]'
        }

        metadataSources { artifact() } 
    }

    maven { url = 'https://www.cursemaven.com' }
}

dependencies {
    minecraft "net.minecraftforge:forge:${mc_version}-${forge_version}"
    implementation "org.spongepowered:mixin:${mixin_version}"
    annotationProcessor "org.spongepowered:mixin:${mixin_version}:processor"

    atDependencies fg.deobf('meldexun:RenderLib:1.12.2-1.3.1@jar')

    buildDependencies fg.deobf('curse.maven:ChunkAnimator-236484:3850023')
    buildDependencies fg.deobf('curse.maven:FluidloggedAPI-485654:3956311')
    buildDependencies fg.deobf('curse.maven:CubicChunks-292243:3546640')
    buildDependencies fg.deobf('curse.maven:CensoredASM-460609:4800875')
}

minecraft {
    mappings channel: "${mappings_channel}", version: "${mappings_version}"

    accessTransformer = files(
        'src/main/resources/META-INF/accesstransformer.cfg',
        files(configurations.atDependencies.collect { zipTree(it) }).getAsFileTree().matching {
            include 'META-INF/accesstransformer.cfg'
        }
    )

    runs {
        client {
            taskName "runClient ${mod_name} ${mc_version}"
            environment 'MC_VERSION', "${mc_version}"
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'info'
            property 'fml.coreMods.load', "${mod_plugin_class},meldexun.renderlib.asm.RenderLibPlugin"
        }

        server {
            taskName "runServer ${mod_name} ${mc_version}"
            environment 'MC_VERSION', "${mc_version}"
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'info'
        }
    }
}

mixin {
    add sourceSets.main, "mixins.${mod_id}.refmap.json"
    config "mixins.${mod_id}.json"
}

processResources {
    inputs.property('version', "${mod_stage}" != "" ? "${mod_version}-${mod_stage}" : "${mod_version}")

    filesMatching('mcmod.info') {
        expand version: inputs.properties.get('version')
    }
}

jar {
    archiveBaseName = mod_name
    archiveAppendix = mc_version
    archiveVersion = mod_version
    archiveClassifier = mod_stage

    from {
        configurations.includedDependencies.collect { it.isDirectory() ? it : zipTree(it) }
    }

    manifest {
        attributes 'ModSide': 'CLIENT'
        attributes 'FMLAT': 'accesstransformer.cfg'
        attributes 'FMLCorePlugin': "${mod_plugin_class}"
        attributes 'FMLCorePluginContainsFMLMod': 'true'
        attributes 'ForceLoadAsMod': 'true'
        attributes 'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker'
        attributes 'TweakOrder': '0'
        attributes 'MixinConfigs': "mixins.${mod_id}.json"
    }
}

jar.finalizedBy('reobfJar')

compileJava {
    classpath = sourceSets.main.compileClasspath + configurations.buildDependencies
}
